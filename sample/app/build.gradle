apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.novoda.staticanalysis.sample"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

apply plugin: com.novoda.staticanalysis.StaticAnalysisPlugin
apply plugin: 'io.gitlab.arturbosch.detekt'

staticAnalysis {

    penalty none

    checkstyle {
        toolVersion '8.8'
        exclude project.fileTree('src/test/java')
        configFile rootProject.file('team-props/checkstyle-modules.xml')
    }

    pmd {
        toolVersion '6.1.0'
        exclude project.fileTree('src/test/java')

        ruleSetFiles = rootProject.files('team-props/pmd-rules.xml')
    }

    findbugs {
        toolVersion '3.0.1'
        excludeFilter rootProject.file('team-props/findbugs-excludes.xml')
    }

    lintOptions {
        lintConfig rootProject.file('team-props/lint-config.xml')
        checkReleaseBuilds false
        warningsAsErrors true
    }

    detekt {
        profile('main') {
            input = "$projectDir/src/main/java"
            config = rootProject.file('team-props/detekt-config.yml')
            filters = '.*test.*,.*/resources/.*,.*/tmp/.*'
            output = "$projectDir/build/reports/detekt"
        }
    }

}
